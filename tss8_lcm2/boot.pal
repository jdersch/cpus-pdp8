/ RK05 BOOTSTRAP VERSION 1.0 (18-NOV-19)
/
/

/ THIS BOOTSTRAP LOADS INIT FROM TRACK 2 INTO FIELD 2
/ AND THEN BEGINS EXECUTION AT 24200.
/ ENSURE PDP-8 IS CLEARED BEFORE EXECUTION. 

DSKP=   6741
DCLR=   6742
DLAG=   6743
DLCA=   6744
DRST=   6745
DLDC=   6746

*200

START, 	TAD 	CADDR
	DLCA
	TAD	CMD
	DLDC   
	TAD	DADDR
	DLAG
WAIT,	DSKP
	JMP WAIT	/ WAIT FOR READ TO COMPLETE 
	CLA
	TAD 	CADDR
	TAD	C0400	/ NEXT CORE ADDRESS
	DCA	CADDR
	ISZ	DADDR	/ NEXT DISK ADDRESS
	ISZ	COUNT
	JMP	START	/ NEXT SECTOR 
	CDI	20	
	JMP I	ENTRY	/ AND WE'RE OFF. 	

DADDR,	0040		/ TRACK 2
CADDR, 	0		/ CORE ADDRESS
CMD,	0020		/ READ, TO FIELD 2, DRIVE 0, NO INTERRUPT.
COUNT, 	-16		/ COUNT OF SECTORS TO BE READ
ENTRY,	4200		/ ENTRYPOINT FOR INIT
C0400,	0400
